# The tests of libraries for C. Build the libraries geo and other with
# antic --lib, then compile and run C programs against them. Run with
# cmake -P and these values:
#   ANTIC     the antic executable
#   LLVM_MC   the llvm-mc executable
#   LLVM_AR   the llvm-ar executable
#   RUNTIME   the runtime directory
#   SOURCES   tests/clib
#   DUMP      tests/dump, with the headers that chapter 25 prints
#   WORK      a directory for the output
#   CASE      static, shared, exports, two, loader, header, bundle,
#             classes, failing or tuples
#   CC        the C compiler of the build, with its options
#   CXX       the same compiler for C++, which checks the headers

set(HOST_SHARED_SUFFIX ".so")
if(APPLE)
    set(HOST_SHARED_SUFFIX ".dylib")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/program_output.cmake")

function(run)
    execute_process(COMMAND ${ARGN} RESULT_VARIABLE status
        OUTPUT_VARIABLE out ERROR_VARIABLE err ENCODING NONE)
    if(NOT status EQUAL 0)
        message(FATAL_ERROR "${ARGN} failed with ${status}\n${out}${err}")
    endif()
    set(run_out "${out}" PARENT_SCOPE)
endfunction()

# Build library name as kind, static or shared, into dir.
function(library name kind dir)
    file(MAKE_DIRECTORY "${dir}")
    if(kind STREQUAL "shared")
        set(file "${dir}/lib${name}${HOST_SHARED_SUFFIX}")
    else()
        set(file "${dir}/lib${name}.a")
    endif()
    run("${ANTIC}" --lib ${kind} ${ARGN} --llvm-mc "${LLVM_MC}"
        --llvm-ar "${LLVM_AR}" --runtime "${RUNTIME}" -I "${SOURCES}"
        -o "${file}" "${SOURCES}/com/example/${name}.anti")
    set(run_out "${run_out}" PARENT_SCOPE)
endfunction()

# Compare the generated header with the one that chapter 25 prints.
function(expect_header header expected)
    file(READ "${header}" got)
    file(READ "${DUMP}/${expected}" wanted)
    if(NOT got STREQUAL wanted)
        message(FATAL_ERROR "${header} differs from ${DUMP}/${expected}\n${got}")
    endif()
endfunction()

# Fail unless the program of ARGN exits with 0 and prints <wanted>, byte
# for byte.
function(expect_printed wanted)
    program_output(got status "${dir}/printed.stdout" ${ARGN})
    string(HEX "${wanted}" want)
    if(NOT status EQUAL 0 OR NOT got STREQUAL want)
        file(READ "${dir}/printed.stdout" text)
        message(FATAL_ERROR "${ARGN} exited with ${status} and printed\n${text}"
                            "expected\n${wanted}")
    endif()
endfunction()

function(expect_output program expected)
    file(READ "${expected}" wanted)
    expect_printed("${wanted}" "${program}")
endfunction()

set(dir "${WORK}/${CASE}")
file(REMOVE_RECURSE "${dir}")

if(CASE STREQUAL "static")
    library(geo static "${dir}")
    # The printed line links main.c with the archive and the runtime.
    string(STRIP "${run_out}" line)
    if(NOT line MATCHES "^cc main.c ${dir}/libgeo.a ${RUNTIME}/lib/[a-z0-9_-]+/[a-z0-9.]+/libanti_rt.a")
        message(FATAL_ERROR "unexpected link line: ${line}")
    endif()
    # The archive keeps the copy of the package header in an object.
    run("${LLVM_AR}" t "${dir}/libgeo.a")
    if(NOT run_out MATCHES "geo.package.o")
        message(FATAL_ERROR "the archive holds no package header\n${run_out}")
    endif()
    string(REPLACE "cc main.c " "" inputs "${line}")
    separate_arguments(inputs UNIX_COMMAND "${inputs}")
    run(${CC} -I "${dir}" "${SOURCES}/roundtrip.c" ${inputs} -o "${dir}/roundtrip")
    expect_output("${dir}/roundtrip" "${SOURCES}/roundtrip.expected")
elseif(CASE STREQUAL "classes")
    # A C program builds an Anti class, calls through its table and ends
    # it. The header compiles as C11 and as C++17.
    library(canvas static "${dir}")
    expect_header("${dir}/canvas.h" canvas.h)
    string(STRIP "${run_out}" line)
    string(REGEX MATCH "[^ ]*libanti_rt.a" runtime_library "${line}")
    run(${CC} -std=c11 -I "${dir}" "${SOURCES}/canvas.c" "${dir}/libcanvas.a"
        "${runtime_library}" -o "${dir}/canvas")
    expect_output("${dir}/canvas" "${SOURCES}/canvas.expected")
    # The copy finds ../binary_stdio.h through the directory of canvas.c.
    configure_file("${SOURCES}/canvas.c" "${dir}/canvas.cpp" COPYONLY)
    run(${CXX} -std=c++17 -I "${dir}" -I "${SOURCES}" "${dir}/canvas.cpp" "${dir}/libcanvas.a"
        "${runtime_library}" -o "${dir}/canvaspp")
    expect_output("${dir}/canvaspp" "${SOURCES}/canvas.expected")
elseif(CASE STREQUAL "failing")
    # Two `may fail` functions cross to C as `?*Error f(args, R *out)`, and
    # the header says in a comment that each may fail.
    library(failing static "${dir}")
    expect_header("${dir}/failing.h" failing.h)
    string(STRIP "${run_out}" line)
    string(REGEX MATCH "[^ ]*libanti_rt.a" runtime_library "${line}")
    run(${CC} -std=c11 -Wall -Werror -I "${dir}" "${SOURCES}/failing.c"
        "${dir}/libfailing.a" "${runtime_library}" -o "${dir}/failing")
    expect_output("${dir}/failing" "${SOURCES}/failing.expected")
elseif(CASE STREQUAL "tuples")
    # A tuple of an exported signature crosses as the struct the header
    # writes for it, one per distinct tuple, and C builds one of its own.
    library(tuples static "${dir}")
    expect_header("${dir}/tuples.h" tuples.h)
    string(STRIP "${run_out}" line)
    string(REGEX MATCH "[^ ]*libanti_rt.a" runtime_library "${line}")
    run(${CC} -std=c11 -Wall -Werror -I "${dir}" "${SOURCES}/tuples.c"
        "${dir}/libtuples.a" "${runtime_library}" -o "${dir}/tuples")
    expect_output("${dir}/tuples" "${SOURCES}/tuples.expected")
elseif(CASE STREQUAL "shared")
    library(geo shared "${dir}")
    run(${CC} -I "${dir}" "${SOURCES}/roundtrip.c"
        "${dir}/libgeo${HOST_SHARED_SUFFIX}" -o "${dir}/roundtrip")
    expect_output("${dir}/roundtrip" "${SOURCES}/roundtrip.expected")
elseif(CASE STREQUAL "exports")
    library(geo shared "${dir}")
    if(APPLE)
        run(nm -gU "${dir}/libgeo.dylib")
    else()
        run(nm -D --defined-only "${dir}/libgeo.so")
    endif()
    string(REGEX MATCHALL "[A-Za-z_][A-Za-z0-9_.]*\n" symbols "${run_out}")
    set(names "")
    foreach(symbol IN LISTS symbols)
        string(STRIP "${symbol}" symbol)
        string(REGEX REPLACE "^_" "" symbol "${symbol}")
        list(APPEND names "${symbol}")
    endforeach()
    list(SORT names)
    set(wanted anti_licenses geo_apply geo_dot geo_half geo_layer geo_ready
        geo_scale)
    if(NOT names STREQUAL wanted)
        message(FATAL_ERROR "exports: ${names}\nexpected: ${wanted}")
    endif()
elseif(CASE STREQUAL "two")
    library(geo shared "${dir}/shared")
    library(other shared "${dir}/shared")
    run(${CC} -I "${dir}/shared" "${SOURCES}/twolibs.c"
        "${dir}/shared/libgeo${HOST_SHARED_SUFFIX}"
        "${dir}/shared/libother${HOST_SHARED_SUFFIX}" -o "${dir}/two_shared")
    expect_printed("10\n" "${dir}/two_shared")
    library(geo static "${dir}/static")
    library(other static "${dir}/static")
    # The printed link line names the runtime library of the host.
    string(REGEX MATCH "[^ ]*libanti_rt.a" runtime_library "${run_out}")
    run(${CC} -I "${dir}/static" "${SOURCES}/twolibs.c" "${dir}/static/libgeo.a"
        "${dir}/static/libother.a" ${runtime_library} -o "${dir}/two_static")
    expect_printed("10\n" "${dir}/two_static")
elseif(CASE STREQUAL "loader")
    library(geo shared "${dir}")
    run(${CC} "${SOURCES}/loader.c" -o "${dir}/loader")
    expect_printed("1\n" "${dir}/loader" "${dir}/libgeo${HOST_SHARED_SUFFIX}")
elseif(CASE STREQUAL "header")
    library(geo static "${dir}")
    expect_header("${dir}/geo.h" geo.h)
    library(shapes static "${dir}")
    expect_header("${dir}/shapes.h" shapes.h)
    run(${CC} -std=c11 -Wall -Werror -I "${dir}" -fsyntax-only
        "${SOURCES}/header.c")
    run(${CXX} -std=c++17 -Wall -Werror -I "${dir}" -fsyntax-only
        "${SOURCES}/header.cpp")
    run(${CC} -std=c11 -Wall -Werror -I "${dir}" -fsyntax-only
        "${SOURCES}/shapes.c")
    run(${CXX} -std=c++17 -Wall -Werror -I "${dir}" -fsyntax-only
        "${SOURCES}/shapes.cpp")
elseif(CASE STREQUAL "bundle")
    library(geo static "${dir}" --bundle-runtime)
    expect_header("${dir}/geo.h" geo.bundle.h)
    string(STRIP "${run_out}" line)
    # The driver puts the system libraries of the target on the line, and
    # a Linux program links pthread and the maths library.
    set(system "")
    if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Linux")
        set(system " -lpthread -lm")
    endif()
    if(NOT line STREQUAL "cc main.c ${dir}/libgeo.a${system}")
        message(FATAL_ERROR "unexpected link line: ${line}")
    endif()
    library(other static "${dir}" --bundle-runtime)
    execute_process(
        COMMAND ${CC} -I "${dir}" "${SOURCES}/twolibs.c" "${dir}/libgeo.a"
                "${dir}/libother.a" -o "${dir}/two_bundled"
        RESULT_VARIABLE status OUTPUT_VARIABLE out ERROR_VARIABLE err ENCODING NONE)
    if(status EQUAL 0 OR NOT "${out}${err}" MATCHES "duplicate symbol|multiple definition")
        message(FATAL_ERROR "two bundled runtimes linked: ${status}\n${out}${err}")
    endif()
    if(NOT "${out}${err}" MATCHES "anti_rt")
        message(FATAL_ERROR "the duplicate does not name the runtime\n${out}${err}")
    endif()
    # A library that reads the notice links against the stub of the bundle
    # and reports an empty notice.
    library(notice static "${dir}/notice" --bundle-runtime)
    run(${CC} -I "${dir}/notice" "${SOURCES}/notice.c" "${dir}/notice/libnotice.a"
        -o "${dir}/notice/notice")
    expect_printed("0\n" "${dir}/notice/notice")
else()
    message(FATAL_ERROR "unknown CASE ${CASE}")
endif()
